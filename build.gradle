plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

group = project.maven_group
version = project.mod_version

base {
    archivesName = project.archives_name
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

loom {
    silentMojangMappingsLicense()
    forge {
        mixinConfigs = [
                "mbe.mixins.json"
        ]
    }
}

repositories {
    flatDir {
        dir 'libs'
    }
    mavenCentral()
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = 'GTCEu Maven'
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
        }
    }
    maven {
        name 'FirstDarkDev'
        url "https://maven.firstdarkdev.xyz/snapshots/"
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.tterrag.com/"
        content {
            includeGroup("com.jozufozu.flywheel")
            includeGroup("com.tterrag.registrate")
            includeGroup("com.simibubi.create")
        }
    }
    maven {
        url = "https://maven.saps.dev/releases/"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
    maven {
        url "https://maven.teamresourceful.com/repository/maven-public/"
    }
    maven {
        name = "Modmaven"
        url = uri("https://modmaven.dev/")
        content {
            includeGroup("appeng")
        }
    }
    maven {
        url 'https://maven.blakesmods.com'
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
}
dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    forge "net.minecraftforge:forge:$project.forge_version"
    //layered mappings - Mojmap names, parchment and QM docs and parameters
    mappings loom.layered {
        it.parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_mappings}@zip")
        it.officialMojangMappings { nameSyntheticMembers = false }
    }

    //JEI
    modCompileOnly("mezz.jei:jei-${project.minecraft_version}-forge-api:${project.jei_version}")
    modCompileOnly("mezz.jei:jei-${project.minecraft_version}-common-api:${project.jei_version}")
    modRuntimeOnly("mezz.jei:jei-${project.minecraft_version}-forge:${jei_version}")
    //GregTech
    modImplementation("com.gregtechceu.gtceu:gtceu-$project.minecraft_version:$project.gtceu_version") { transitive = false }
    modImplementation("curse.maven:gtmthings-1104310:6167532")
    modImplementation("com.lowdragmc.ldlib:ldlib-forge-$project.minecraft_version:$project.ldlib_version") { transitive = false }
    modImplementation("dev.toma.configuration:configuration-forge-${project.minecraft_version}:${project.configuration_version}")
    modImplementation("com.tterrag.registrate:Registrate:${project.registrate_version}")
    modImplementation("dev.latvian.mods:kubejs-forge:${project.kubejs_version}")
    //Mixin Extras
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${project.mixinextras_version}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${project.mixinextras_version}"))
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    //Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    //Ad Astra
    modImplementation("com.teamresourceful.resourcefullib:resourcefullib-forge-${project.minecraft_version}:${project.resourcefullib_version}")
    modImplementation("com.teamresourceful.resourcefulconfig:resourcefulconfig-forge-${project.minecraft_version}:${project.resourcefulconfig_version}")
    modImplementation("earth.terrarium.botarium:botarium-forge-${project.minecraft_version}:${project.botarium_version}")
    modImplementation("earth.terrarium.adastra:ad_astra-forge-${project.minecraft_version}:${ad_astra_version}") {transitive false}
    modImplementation("curse.maven:ad-astra-rocketed-996980:5245376")
    forgeRuntimeLibrary("com.teamresourceful:yabn:1.0.3")
    forgeRuntimeLibrary("com.teamresourceful:bytecodecs:1.0.2")
    //Twilight Forest
    modImplementation("curse.maven:the-twilight-forest-227639:5468648") {transitive false}
    //Jade
    modImplementation("curse.maven:jade-324717:5876199")
    //AE2
    modImplementation 'appeng:appliedenergistics2-forge:15.2.9'
    //Misc
    modImplementation "curse.maven:construction-wand-399558:4684054"
    modCompileOnly "curse.maven:re-avaritia-623969:5888767"
}

    processResources {
        inputs.property 'version', project.version

        filesMatching('META-INF/mods.toml') {
            expand version: project.version
        }
    }

    java {

        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {}
    }
